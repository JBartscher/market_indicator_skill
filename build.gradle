import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'java'

// project level config
sourceSets.main.java.srcDir 'src'
sourceCompatibility = 1.7
targetCompatibility = 1.7

task fetchDeps(type: Exec) {
	description 'Fetch and install LazyLib and GraphicsLib before compilation.'
	commandLine 'sh', 'util/fetch-deps.sh'
}
compileJava.dependsOn fetchDeps

build {
    doLast {
        copy {
            from jar
            into '.'
        }
    }
}

task run(type: Exec) {
	description 'Run Starsector'
	dependsOn build
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        workingDir '../../starsector-core'
        commandLine 'cmd', '/c', 'start', 'starsector.bat'
    } else {
        workingDir '../..'
        commandLine 'sh', 'starsector.sh'
    }
}

task release(type: Exec) {
	description 'Create releaseable zip file.'
	commandLine 'sh', 'util/create-release.sh'
}

// nuke everything and start over
clean {
	delete fileTree('.') { include '*.jar' }
}

task wrapper(type: Wrapper) {
	description 'Create a new Gradle wrapper.'
    gradleVersion = '3.2.1' 
}

// compile time dependencies for most mods 
dependencies {
    compile files('../GraphicsLib/jars/Graphics.jar')
    compile files('../LazyLib/jars/LazyLib.jar')
    // everything in starsector main folder
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        compile fileTree(dir: '../../starsector-core/', include: '*.jar')
    } else {
        compile fileTree(dir: '../../', include: '*.jar')
    }
}
