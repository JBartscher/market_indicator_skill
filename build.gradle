apply plugin: 'java'

// project level config
sourceSets.main.java.srcDir 'src'
sourceCompatibility = 1.7
targetCompatibility = 1.7

task fetchDeps(type: Exec) {
	description 'Fetch and install LazyLib and GraphicsLib before compilation.'
	commandLine 'sh', 'util/fetch-deps.sh'
}
compileJava.dependsOn fetchDeps

task copyJar(type: Copy) {
	description 'Copy compiled jar to current working directory.'
	from 'build/libs/'
	include '*.jar'
	into '.'
}
build.dependsOn copyJar

task run(type: Exec) {
	description 'Run Starsector'
	dependsOn build
	workingDir '../..' 
	commandLine 'sh', 'starsector.sh'
}

task release(type: Exec) {
	description 'Create releaseable zip file.'
	commandLine 'sh', 'util/create-release.sh'
}

// nuke everything and start over
clean {
	delete fileTree('.') { include '*.jar' }
}

task wrapper(type: Wrapper) {
	description 'Create a new Gradle wrapper.'
    gradleVersion = '3.2.1' 
}

// compile time dependencies for most mods 
dependencies {
	compile files('../GraphicsLib/jars/Graphics.jar')
	compile files('../LazyLib/jars/LazyLib.jar')
	// everything in starsector main folder
	compile fileTree(dir: '../../', include: ['*.jar'])
}

